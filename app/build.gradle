apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {

    def versionMajor = 1
    def versionMinor = 1
    def versionPatch = 7
    def versionBuild = 0

    compileSdkVersion 28
    defaultConfig {
        applicationId "io.github.tormundsmember.easyflashcards"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' (DEBUG)'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean","isCrashReportingEnabled","false"
            buildConfigField "boolean", "isDebugbuild", "true"
            buildConfigField "boolean", "isInternalReleaseBuild", "false"
            buildConfigField "boolean", "isProductionBuild", "false"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean","isCrashReportingEnabled","true"
            buildConfigField "boolean", "isDebugbuild", "false"
            buildConfigField "boolean", "isInternalReleaseBuild", "false"
            buildConfigField "boolean", "isProductionBuild", "true"
        }

        releaseInternal {

            initWith(buildTypes.release)

            applicationIdSuffix '.releaseInternal'
            versionNameSuffix ' (CL)'

            debuggable true
            minifyEnabled false
            buildConfigField "boolean","isCrashReportingEnabled","true"
            buildConfigField "boolean", "isDebugbuild", "false"
            buildConfigField "boolean", "isInternalReleaseBuild", "true"
            buildConfigField "boolean", "isProductionBuild", "false"
        }
    }
    androidExtensions {
        experimental = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {

    def room_version = "2.2.3"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.2.0-alpha04'
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation 'com.github.Zhuinden:simple-stack:2.2.4'
    implementation 'androidx.browser:browser:1.2.0'

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    testImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

}
